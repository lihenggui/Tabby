pluginManagement {
  includeBuild("build-logic")
  repositories {
    maven("https://central.sonatype.com/repository/maven-snapshots/") {
      mavenContent { includeGroupAndSubgroups("io.github.goooler.golang") }
    }
    google {
      mavenContent {
        includeGroupAndSubgroups("androidx")
        includeGroupAndSubgroups("com.android")
        includeGroupAndSubgroups("com.google")
      }
    }
    mavenCentral()
    gradlePluginPortal()
  }
}

dependencyResolutionManagement {
  repositoriesMode = RepositoriesMode.FAIL_ON_PROJECT_REPOS
  repositories {
    google {
      mavenContent {
        includeGroupAndSubgroups("androidx")
        includeGroupAndSubgroups("com.android")
        includeGroupAndSubgroups("com.google")
      }
    }
    mavenCentral()
  }
}

plugins { id("com.gradle.develocity") version "4.4.2" }

develocity {
  buildScan {
    termsOfUseUrl = "https://gradle.com/help/legal-terms-of-use"
    termsOfUseAgree = "yes"
    val isCI = providers.environmentVariable("CI").isPresent
    publishing.onlyIf { isCI }
  }
}

rootProject.name = "Tabby"

enableFeaturePreview("TYPESAFE_PROJECT_ACCESSORS")

enableFeaturePreview("STABLE_CONFIGURATION_CACHE")

include(
  ":app",
  ":app:desktop",
  ":app:shared",
  ":core",
  ":core:common",
  ":core:database",
  ":core:engine-api",
  ":core:engine-android",
  ":core:engine-desktop",
  ":core:engine-ios",
  ":core:model",
  ":core:network",
  ":core:settings-store",
  ":service",
  ":common",
  ":glue",
  ":ui:shared",
  ":ui:crash",
  ":ui:home",
  ":ui:log",
  ":ui:proxy",
  ":ui:profile",
  ":ui:settings",
)
